# for use with configure_package_config_file()
# see https://cmake.org/cmake/help/latest/module/CMakePackageConfigHelpers.html
# also see https://stackoverflow.com/questions/75845254/how-does-cmake-s-configure-package-config-file-work
# required input: 
# ConfigMasterName : name of the master package

@PACKAGE_INIT@

# add dependecy check
cmake_minimum_required(VERSION 3.20)
include(CMakeFindDependencyMacro)
# set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${PACKAGE_PREFIX_DIR}/cmake")
list(APPEND CMAKE_MODULE_PATH "${PACKAGE_PREFIX_DIR}/cmake")
include(${PACKAGE_PREFIX_DIR}/cmake/igcclib-dependency.cmake)
include(${PACKAGE_PREFIX_DIR}/cmake/component-helpers.cmake)

# this file will generate the igcclib-config.cmake in CMAKE_INSTALL_PREFIX/lib/cmake
# to process find_package(igcclib COMPONENTS xxx, yyy, ...) calls
# the xxx component can be found at ./igcclib/xxx-config.cmake
set(master_name @ConfigMasterName@)

# asking to find components?
set(comps_asked ${${master_name}_FIND_COMPONENTS})
get_igcclib_component_dependencies(COMPONENTS ${comps_asked} OUTPUT_VAR comps_to_load)
message(STATUS "${master_name} components to load: ${comps_to_load}")
# load all components
# BUGFIX: PACKAGE_PREFIX_DIR gets changed by include, don't know why
set(pkg_dir ${PACKAGE_PREFIX_DIR})
foreach(comp ${comps_to_load})
    message(STATUS "Loading ${master_name}::${comp}")
    get_component_cmake_output_dir(${comp} comp_output_dir)
    get_component_config_filename(${comp} comp_config_filename)
    
    include(${pkg_dir}/${comp_output_dir}/${comp_config_filename})
endforeach()
