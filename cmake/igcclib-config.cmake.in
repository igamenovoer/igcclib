# for use with configure_package_config_file()
# see https://cmake.org/cmake/help/latest/module/CMakePackageConfigHelpers.html
# also see https://stackoverflow.com/questions/75845254/how-does-cmake-s-configure-package-config-file-work
# required input: 
# ConfigMasterName : name of the master package

@PACKAGE_INIT@

# add dependecy check
cmake_minimum_required(VERSION 3.20)

# this file will generate the igcclib-config.cmake in CMAKE_INSTALL_PREFIX/lib/cmake
# to process find_package(igcclib COMPONENTS xxx, yyy, ...) calls
# the xxx component can be found at ./igcclib/xxx-config.cmake
include(CMakeFindDependencyMacro)

# prepend @ConfigMasterName@ to every variable defined in this file
# so that they do not conflict with other projects
set(@ConfigMasterName@_package_dir ${PACKAGE_PREFIX_DIR})
set(@ConfigMasterName@_project_name @ConfigMasterName@)

list(APPEND CMAKE_MODULE_PATH "${@ConfigMasterName@_package_dir}/cmake/${@ConfigMasterName@_project_name}")
include(igcclib-dependency)
include(igcclib-component-helpers)

# asking to find components?
set(comps_asked ${${@ConfigMasterName@_project_name}_FIND_COMPONENTS})
get_igcclib_component_dependencies(COMPONENTS ${comps_asked} OUTPUT_VAR comps_to_load)
message(STATUS "${@ConfigMasterName@_project_name} components to load: ${comps_to_load}")

# load all components
foreach(comp ${comps_to_load})
    message(STATUS "Loading ${@ConfigMasterName@_project_name}::${comp}")
    get_component_cmake_output_dir(${comp} comp_output_dir)
    get_component_config_filename(${comp} comp_config_filename)
    
    include(${@ConfigMasterName@_package_dir}/${comp_output_dir}/${comp_config_filename})
endforeach()
